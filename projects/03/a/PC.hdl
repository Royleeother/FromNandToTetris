// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Put your code here:
    // Pin priority: reset, load, inc, so we should implement inc load reset
    // inc
    Add16(a=outin, b[0]=true, out=addO);
    Mux16(a=outin, b=addO, sel=inc, out=incO);
    // load
    Mux16(a=incO, b=in, sel=load, out=loadO);  
    // reset
    Mux16(a=loadO, b=false, sel=reset, out=resetO);
    
    // circle
    Register(in=resetO, load=true, out= outin, out=out);
}
